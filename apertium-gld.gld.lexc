Multichar_Symbols

%<n%>    ! Noun
%<det%>  
%<prn%>  
%<ij%>
%<adv%>  
%<ideo%>

%<v%>    ! 

%<tv%>   !
%<iv%>   !

%<p1%>
%<p2%>
%<p3%>

%<pl%>   ! Plural
%<sg%>   ! Singular

%<emph%> 

%<nom%>
%<acc%>
%<dat%>
%<dir%>   ! Directive
%<abl%>
%<ben%>   ! Benefactive / destinative
%<ins%>
%<loc%>

%<asrt%>  ! Assertive

%<pres%>
%<past%>

%<gna_purp%>
%<gna_sim%>
%<gna_con%>
%<gna_cond%>
%<gna_cond2%>

%<gpr_pres%>
%<ger_pres%>

%<iter%> ! One more time

%<px1sg%>
%<px2sg%>
%<px3sg%>
%<px1pl%>
%<px2pl%>
%<px3pl%>

%<rxsg%>
%<rxpl%>

%{б%}          ! б / -
%{в%}          ! б / в 
%{н%}          ! н / -
%{к%}          ! к / ч
%{A%}          ! а / э
%{I%}          ! и / -
%{O%}          ! о / у 
%{i%}          ! и / -
%{w%}          ! б / в / -
%{р%}          ! р / а 
%{д%}          ! д / т           ! 
%{й%}          ! 0               ! Trigger for yotised consonants
%{м%}          ! м               ! Trigger for labial deletion
%{п%}          ! п               ! Trigger for labial deletion

%{ъ%}
%{ь%}

%{ː%}

%-
%+
%> 

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                       M O R P H O T A C T I C S                         !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root

Determiners ;
Pronouns  ;
Numerals ;
Postpositions ; 
Nouns ;
ProperNouns ;
Adverbs ;
Adjectives ;
Verbs ;
Ideophones ;
Interjections ; 
Punctuation ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON K

%+да%<emph%>:%>%-д%{A%} # ;
%+а%<emph%>:%>%{A%} # ;
%+гоа%<emph%>:%>%-г%{O%}%{A%} # ;
%+гоани%<emph%>:%>%-г%{O%}%{A%}ни # ;
# ;

LEXICON PERS-COMMON 

%<p1%>%<sg%>:%>%{б%}и K ;
%<p1%>%<pl%>:%>пу K ;
%<p2%>%<pl%>:%>су K ;

LEXICON PERS-INDIC

%<p1%>%<sg%>:%>%{б%}%{i%} K ; ! Dir/LR
%<p2%>%<sg%>:%>си K ;
%<p3%>%<sg%>:%>ни K ;
%<p3%>%<pl%>:%>чи K ;
%<p3%>%<pl%>:%>л K ; ! Dir/LR

PERS-COMMON ;

LEXICON PERS-ASSERT

%<p2%>%<sg%>:%>си K ;
%<p3%>%<sg%>: K ;
%<p3%>%<pl%>:%>л K ; 

PERS-COMMON ;

LEXICON PERS-ASSERT-PRES

%<p2%>%<sg%>:%>чи K ;
%<p3%>%<sg%>:%> K ;
%<p3%>%<pl%>:%>л K ; 

PERS-COMMON ; 

LEXICON PERS-ASSERT-FUT 

%<p2%>%<sg%>:%>чи K ;
PERS-COMMON ; 

LEXICON TENSE-12-COMMON

%<past%>:%>х%{A%}%{н%} PERS-INDIC ;
%<past%>:%>х%{A%}%{н%} K ;
%<past%>:%>х%{A%} # ; ! Dir/LR

%<subj%>:%>%{м%}ч%{A%}%> PERS-ASSERT ; 

LEXICON ASSERT-COMMON

%<fut%>:%>д%{й%}%{A%}%>%{н%}                       PERS-ASSERT-FUT ; 
%<fut%>%<p3%>%<sg%>:%>д%{й%}%{A%}%>р%{A%}%{н%}     # ;
%<fut%>%<p3%>%<pl%>:%>д%{й%}%{A%}%>р%{A%}%{н%}%>л  # ;

!%<fut%>:%>д%{й%}%{а%}%>%{р%}%{A%}%{н%} PERS-ASSERT-PRES ; ! FIXME: {A} is long

LEXICON ASSERT-1

%<pres%>:%>%{р%}%{A%}%{н%} PERS-ASSERT-PRES ;
%<past%>:%>к%{A%} PERS-ASSERT ; 

ASSERT-COMMON ; 

LEXICON TENSE-1 

%<pres%>:%>%{I%} PERS-INDIC ;
%<pres%>:%>%{I%} # ;

%<gna_purp%>:%>г%{O%} POSS-OBL  ;
%<gna_cond2%>:%>%{O%}чи POSS-OBL-NOREFL ; 
%<gna_cond2%>:%>%{w%}%{O%}чи POSS-OBL-NOREFL ;  ! Dir/LR

%<gna_con%>:%>р%{A%} # ;
%<gna_sim%>%<sg%>:%>%{м%}и # ; 
%<gna_sim%>%<pl%>:%>%{м%}%{А%}ри # ; 
%<gna_cond%>%<sg%>:%>%{п%}и # ; 
%<gna_cond%>%<pl%>:%>%{п%}%{A%}ри # ; 
%<gpr_pres%>:%>%{I%} # ;
%<ger_pres%>:%>%{I%} CASE ;

%<asrt%>: ASSERT-1 ; 

TENSE-12-COMMON  ;

LEXICON ASSERT-2

%<pres%>:%>р%{A%}%{н%} PERS-ASSERT-PRES ;
%<past%>:%>к%{A%} PERS-ASSERT ; 

ASSERT-COMMON ; 

LEXICON TENSE-2

%<pres%>:%>ри PERS-INDIC ;
%<pres%>:%>ри # ; 

%<gna_purp%>:%>г%{O%} POSS-OBL  ;
%<gna_cond2%>:%>%{O%}чи POSS-OBL-NOREFL ; 
%<gna_cond2%>:%>%{w%}%{O%}чи POSS-OBL-NOREFL ;  ! Dir/LR

%<gpr_pres%>:%>ри # ;
%<ger_pres%>:%>ри CASE ;

%<gna_con%>:%>р%{A%} # ;
%<gna_sim%>%<sg%>:%>%{м%}и # ; 
%<gna_sim%>%<pl%>:%>%{м%}%{А%}ри # ; 
%<gna_cond%>%<sg%>:%>%{п%}и # ; 
%<gna_cond%>%<pl%>:%>%{п%}%{A%}ри # ; 

%<asrt%>: ASSERT-2 ; 

TENSE-12-COMMON  ;

LEXICON ASSERT-3

%<pres%>:%>%{д%}%{A%}%{н%} PERS-ASSERT-PRES ;
%<past%>:%>%{к%}%{A%} PERS-ASSERT ; 

ASSERT-COMMON ; 

LEXICON TENSE-3

%<pres%>:%>ди PERS-INDIC ;
%<pres%>:%>ди # ; 

%<gna_purp%>:%>г%{O%} POSS-OBL  ; 
%<gna_purp%>:%>%{п%}%{O%}%>г%{O%} POSS-OBL  ; ! Dir/LR
%<gna_cond2%>:%>%{O%}чи POSS-OBL-NOREFL ; 
%<gna_cond2%>:%>%{w%}%{O%}чи POSS-OBL-NOREFL ;  ! Dir/LR

%<gpr_pres%>:%>ди # ;
%<ger_pres%>:%>ди CASE ;

%<gna_con%>:%>%{д%}%{A%} # ;
%<gna_sim%>%<sg%>:%>%{м%}и # ; 
%<gna_sim%>%<pl%>:%>%{м%}%{А%}ри # ; 
%<gna_cond%>%<sg%>:%>%{п%}и # ; 
%<gna_cond%>%<pl%>:%>%{п%}%{A%}ри # ; 

%<past%>:%>%{к%}и{н%} PERS-INDIC ; 

%<subj%>:%>%{м%}ч%{A%} PERS-ASSERT ; 

%<asrt%>: ASSERT-3 ; 

LEXICON V-DER 

%<iter%>:%>г%{O%} TENSE-1 ;
%<impers%>:%>%{w%}%{O%} TENSE-1 ;

LEXICON V-1-TV

%<v%>%<tv%>: TENSE-1 ; 
%<v%>%<tv%>: V-DER ; 

LEXICON V-2-TV 

%<v%>%<tv%>: TENSE-2 ;
%<v%>%<tv%>: V-DER ; 

LEXICON V-3-TV 

%<v%>%<tv%>: TENSE-3 ;
%<v%>%<tv%>: V-DER ; 

LEXICON V-1-IV

%<v%>%<iv%>: TENSE-1 ; 
%<v%>%<iv%>: V-DER ; 

LEXICON V-2-IV 

%<v%>%<iv%>: TENSE-2 ;
%<v%>%<iv%>: V-DER ; 

LEXICON V-3-IV 

%<v%>%<iv%>: TENSE-3 ;
%<v%>%<iv%>: V-DER ; 

LEXICON V-X-IV 

%<v%>%<iv%>%<pres%>:%>%{I%} PERS-INDIC ;
%<v%>%<iv%>%<past%>:%>%{к%}и{н%} PERS-INDIC ; 
%<v%>%<iv%>%<past%>:%>%{к%}и{н%} # ;
%<v%>%<iv%>: V-DER ; 

LEXICON POSS-ACC

%<px1sg%>:%>и%>в%{A%} K ; 
%<px1pl%>:%>п%{O%}%>в%{A%} K ; 
%<px2sg%>:%>%{б%}%{A%}%>си K ; 
%<px3sg%>:%>%{б%}%{A%}%>ни K ; 
!%<px2pl%>:%>%{б%}%{A%}%>с%{O%} K ; 
%<px2pl%>:%>%{б%}%{A%}%>с%{O%} K ; 
%<px2pl%>:%>%{б%}%{A%}%>су K ; 
%<px3pl%>:%>%{б%}%{A%}чи K ; 
%<rxsg%>:%>и K ;
%<rxpl%>:%>%{б%}%{A%}ри K ;

LEXICON POSS-LOC

%<px1sg%>:%>и%>в%{A%} K ; 
%<px1pl%>:%>п%{O%}%>в%{A%} K ; 
%<px2sg%>:%>си K ; 
%<px3sg%>:%>%{н%}и K ; 
!%<px2pl%>:%>с%{O%} K ; 
%<px2pl%>:%>су K ; 
%<px2pl%>:%>со K ; 
%<px3pl%>:%>чи K ; 
%<rxsg%>:%>и K ;
%<rxpl%>:%>ри K ;

K ;

LEXICON POSS-NOM

%<px1sg%>:%>%{б%}и K ; 
!%<px1pl%>:%>п%{O%} K ; 
%<px1pl%>:%>пу K ; 
%<px1pl%>:%>по K ; 
%<px2sg%>:%>си K ; 
%<px3sg%>:%>%{н%}и K ; 
!%<px2pl%>:%>с%{O%} K ; 
%<px2pl%>:%>су K ; 
%<px2pl%>:%>со K ; 
%<px3pl%>:%>чи K ; 

# ;

LEXICON POSS-1-OBL

%<px1sg%>:%>и%>в%{A%} K ; 
%<px1pl%>:%>п%{O%}%>в%{A%} K ; 

LEXICON POSS-23-OBL

%<px2sg%>:%>%{A%}%>си K ; 
%<px3sg%>:%>%{A%}%>ни K ; 
!%<px2pl%>:%>%{A%}%>с%{O%} K ; 
%<px2pl%>:%>%{A%}%>со K ; 
%<px2pl%>:%>%{A%}%>су K ; 
%<px3pl%>:%>%{A%}%>чи K ; 

LEXICON POSS-REFL 

%<rxsg%>:%>%{б%}и K ;
%<rxpl%>:%>%{A%}ри K ;

LEXICON POSS-OBL

POSS-1-OBL ;
POSS-23-OBL ;
POSS-REFL ;

# ;

LEXICON POSS-OBL-NOREFL

POSS-1-OBL ;
POSS-23-OBL ;

# ;

LEXICON CASE 

%<nom%>: POSS-NOM ;
%<acc%>:%>%{в%}%{A%} # ; 
%<acc%>: POSS-ACC ; 
%<dat%>:%>д%{O%} POSS-OBL ; 
%<dat%>:%>ду # ;  
%<dir%>:%>чи POSS-OBL ; 
%<abl%>:%>ди%{A%}ди POSS-OBL ; 
%<ben%>:%>г%{O%} POSS-OBL ; ! FIXME: -го/-гу [Front], -гу [Back]
%<ins%>:%>ди POSS-OBL ; 

LEXICON NUMBER 

%<sg%>: CASE ; 
%<pl%>:%>с%{A%}л CASE ; 
%<pl%>%<loc%>:%>с%{A%}л%>д%{O%}л%{A%} POSS-LOC ; 

LEXICON N-OGDA

%<n%>: NUMBER ;
%<n%>%<sg%>%<loc%>:%>л%{A%} POSS-LOC ; 

LEXICON N-EKTE

%<n%>%<sg%>:тэ CASE ;
%<n%>%<pl%>:э%>с%{A%}л CASE ;
%<n%>%<sg%>%<loc%>:тэ%>л%{A%} POSS-LOC ; 
%<n%>%<pl%>%<loc%>:э%>с%{A%}л%>д%{O%}л%{A%} POSS-LOC ; 

LEXICON N-NIITSJEN

%<n%>: NUMBER ;
%<n%>%<sg%>%<loc%>:%>д%{O%}л%{A%} POSS-LOC ; 

LEXICON PRON-PERS-12-OBL

%<sg%>%<acc%>:мбинэ # ;
%<sg%>%<ins%>:нди # ;
%<sg%>%<dat%>:нду # ;
%<sg%>%<loc%>:ндулэ # ;
%<sg%>%<dir%>:нчи # ;
%<sg%>%<abl%>:ндиэди # ;

LEXICON PRON-PERS-MI

%<prn%>%<pers%>%<p1%>%<sg%>%<nom%>: # ;
%<prn%>%<pers%>%<p1%>%<sg%>: PRON-PERS-12-OBL ;

LEXICON PRON-PERS-SI

%<prn%>%<pers%>%<p2%>%<sg%>%<nom%>: # ;
%<prn%>%<pers%>%<p2%>%<sg%>: PRON-PERS-12-OBL ;

LEXICON PRON-PERS-BUE

%<prn%>%<pers%>%<p1%>%<pl%>%<nom%>:э # ;
%<prn%>%<pers%>%<p1%>%<pl%>: PRON-PERS-12-OBL ;

LEXICON PRON-PERS-SUE

%<prn%>%<pers%>%<p2%>%<pl%>%<nom%>:э # ;
%<prn%>%<pers%>%<p2%>%<pl%>: PRON-PERS-12-OBL ;

LEXICON PRON-PERS-NYOANI

%<prn%>%<pers%>%<p3%>%<sg%>%<nom%>:ни # ;
%<prn%>%<pers%>%<p3%>%<sg%>%<acc%>:мбани # ;
%<prn%>%<pers%>%<p3%>%<sg%>%<ins%>:ндиани # ;
%<prn%>%<pers%>%<p3%>%<sg%>%<dat%>:ндоани # ;
%<prn%>%<pers%>%<p3%>%<sg%>%<loc%>:ндолани # ;
%<prn%>%<pers%>%<p3%>%<sg%>%<dir%>:нчиани # ;
%<prn%>%<pers%>%<p3%>%<sg%>%<abl%>:ндиадиани # ;

LEXICON PRON-PERS-NYOANTSJI

%<prn%>%<pers%>%<p3%>%<sg%>%<nom%>:нчи # ;
%<prn%>%<pers%>%<p3%>%<sg%>%<acc%>:мбачи # ;
%<prn%>%<pers%>%<p3%>%<sg%>%<ins%>:ндиачи # ;
%<prn%>%<pers%>%<p3%>%<sg%>%<dat%>:ндоачи # ;
%<prn%>%<pers%>%<p3%>%<sg%>%<loc%>:ндолачи # ;
%<prn%>%<pers%>%<p3%>%<sg%>%<dir%>:нчиачи # ;
%<prn%>%<pers%>%<p3%>%<sg%>%<abl%>:ндиадиачи # ;

LEXICON PRON-REFL-MEPI

%<prn%>%<ref%>%<sg%>%<acc%>:пи # ;
%<prn%>%<ref%>%<sg%>%<ins%>:ндии # ;
%<prn%>%<ref%>%<sg%>%<dat%>:ндуи # ;
%<prn%>%<ref%>%<sg%>%<loc%>:ндулэи # ;
%<prn%>%<ref%>%<sg%>%<dir%>:нчии # ;
%<prn%>%<ref%>%<sg%>%<abl%>:ндиэдии # ;

LEXICON PRON-REFL-MEPERI

%<prn%>%<ref%>%<pl%>%<acc%>:пэри # ;
%<prn%>%<ref%>%<pl%>%<ins%>:ндиэри # ;
%<prn%>%<ref%>%<pl%>%<dat%>:ндуэри # ;
%<prn%>%<ref%>%<pl%>%<loc%>:ндулэри # ;
%<prn%>%<ref%>%<pl%>%<dir%>:нчиэри # ;
%<prn%>%<ref%>%<pl%>%<abl%>:ндиэдиэри # ;

LEXICON PRON-DEM 

%<prn%>%<dem%>: CASE ; 

LEXICON NP-ANT-M 

%<np%>%<ant%>%<m%>%<sg%>: # ;

LEXICON A

%<adj%>: # ;

LEXICON POST

%<post%>: # ;

LEXICON POST-OGDA

N-OGDA ; 

LEXICON POST-NIITSJEN 

N-NIITSJEN ; 

LEXICON ADV

%<adv%>: K ;

LEXICON INTERJ

%<ij%>: # ; 

LEXICON IDEO

%<ideo%>: # ; 

LEXICON ADV-ITG

%<adv%>%<itg%>: K ;

LEXICON DET-IND

%<det%>%<ind%>: # ;


LEXICON DET-DEM 

%<det%>%<dem%>: # ;

LEXICON NUM

%<num%>: # ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                          L E X I C O N                                  !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Determiners

эй:эй DET-DEM ; 
мэй:мэй DET-DEM ; 
тэй:тэй DET-DEM ; 

тамача̄:тамача%{ː%} DET-IND ; ! ""

LEXICON Pronouns 

ми:ми PRON-PERS-MI ;
си:си PRON-PERS-SI ;
буэ:бу PRON-PERS-BUE ;
суэ:су PRON-PERS-SUE ;

нёани:нёа PRON-PERS-NYOANI ; 
нёанчи:нёа PRON-PERS-NYOANTSJI ; 

мэ̄пи:мэ PRON-REFL-MEPI ;
мэпэри:мэ PRON-REFL-MEPERI ;

тэй:тэй PRON-DEM ; ! ""

LEXICON Numerals

эм:эм NUM ; ! "один" ! эмун(?)
дюэр:дюэр NUM ; ! "два"
илан:илан NUM ; ! "три"
дуин:дуин NUM ; ! "четыре"
тойнга:тойнга NUM ; ! "пять"
нюнгун:нюнгун NUM ; ! "шесть"
надан:надан NUM ; ! "семь"
дякпун:дякпун NUM ; ! "восемь"
хуюн:хуюн NUM ; ! "девять"
дёан:дёан NUM ; ! "десять"

LEXICON Postpositions 

боакиалани:боакиалани POST ; ! "за пределами"
кирадоани:кирадоани POST ; ! "на краю, у края"

анāдо:ана%{ː%}до POST-OGDA ; ! "без"
далиндо:далиндо POST-OGDA ; ! ""
до̄чи:до%{ː%}чи POST-OGDA ; ! ""
до:до POST-OGDA ; ! "" 
дюлиэлэ:дюлиэлэ POST-OGDA ; ! ""
дякпа:дякпа POST-OGDA ; ! ""
кэ̄ндэли:кэ%{ː%}ндэли POST-OGDA ; ! "вокруг"
талгиа:талгиа POST-OGDA ; ! "напротив"
таяла:таяла POST-OGDA ; ! ""
уектулэ:уектулэ POST-OGDA ; ! "над"
уелэ:уелэ POST-OGDA ; ! "над"
хамиактола:хамиактола POST-OGDA ; ! "вслед за"
хангиала:хангиала POST-OGDA ; ! "в стороне от"
хангиачи:хангиачи POST-OGDA ; ! "в сторону от"
холдондола:холдондола POST-OGDA ; ! "около, возле, рядом, сбоку, у"
холдондо:холдондо POST-OGDA ; ! "сбоку, возле, рядом, около"
эгдиктэ:эгдиктэ POST-OGDA ; ! "большинство, большая часть"
таон:таон POST-NIITSJEN ; ! "каждый"

LEXICON Nouns


огда:огда N-OGDA ; ! "лодка"
ниичэн:ни%{ː%}чэн N-NIITSJEN ; ! "птица"
дэрэ:дэрэ N-OGDA ; ! "стол"
ихон:ихон N-NIITSJEN ; ! "деревня"

таон:таон N-NIITSJEN ; ! ""

на̄:на%{ː%} N-OGDA ; ! "земля"

абаха:абаха N-OGDA ; ! "лист"

эктэ:эк N-EKTE ; ! "женщина"

пиктэ:пиктэ N-OGDA ; ! "ребёнок"

дяка:дяка N-OGDA ; ! "вещь"
усэлтэ:усэлтэ N-OGDA ; ! "зверь"

мапа:мапа N-OGDA ; ! "старик"
мама:мама N-OGDA ; ! "старуха"

най:най N-NIITSJEN ; ! "человек"

боа:боа N-OGDA ; ! "место"
хасар:хасар N-NIITSJEN ; ! "крыло"

соли:соли N-OGDA ; ! "лиса"
хусэгден:хусэгден N-NIITSJEN ; ! "охотник"

дёлока̄н:дёлока%{ː%}н N-NIITSJEN ; ! "" ! Der/Dimin

енгур:енгур N-NIITSJEN ; ! "волк"

! дёг:дёг 

LEXICON ProperNouns

Уде:Уде NP-ANT-M ; ! ""

LEXICON Adjectives 

дяди:дяди A ; ! "легко"

LEXICON Adverbs

хаоси:хаоси ADV-ITG ; ! "куда"
хаяди:хаяди ADV-ITG ; ! "откуда"
хали:хали ADV-ITG ; ! "когда"
хони:хони ADV-ITG ; ! "как"

гэ:гэ ADV ; ! ""
тэнг:тэнг ADV ; ! "только, очень"
балана:балана ADV ; ! "давно"
тул%-тул:тул%-тул ADV ; ! "все время"

туй:туй ADV ; ! ""
маня̄:маня%{ː%} ADV ; ! "только" 

LEXICON Verbs

дёбо:дёбо V-1-TV ; ! "работать"
ба̄:ба%{ː%} V-2-TV ; ! "находить"
ун:ун V-3-TV ; ! "говорить"
га:га V-3-TV ; ! "собирать"      ! FIXME: Check class IV ?
би:би%{ь%} V-X-IV ; ! "быть"
хӯл:ху%{ː%}л V-3-TV ; ! "лить"
соли:соли V-1-TV ; ! "перемешевать"
деп:деп V-3-TV ; ! "кушать"
деп%<v%>%<tv%>%<gna_purp%>:деб%>д%{O%} POSS-OBL  ; ! Use/Norm

дяпа:дяпа V-1-TV ; ! "брать"

этэхи:этэхи V-1-TV ; ! "беречь"

га̄дё:га%{ː%}дё V-1-TV ; ! "относить"

такто̄ла:такто%{ː%}ла V-1-IV ; ! ""

дуэрэ:дуэрэ V-1-IV ; ! "ходить"
ниэ:ниэ V-2-IV ; ! "входить"

нэ̄:нэ%{ː%} V-2-TV ; ! "класть"

амда:амда V-1-TV ; ! "подражать"

куӈкулэ:куӈкулэ V-1-TV ; ! ""

ниру:ниру V-1-TV ; ! "писать"
туту:туту V-1-IV ; ! "бежать"
энэ:энэ V-1-IV ; ! "идти"
улэси:улэси V-1-TV ; ! "любить" ! FIXME
или:или%{ъ%} V-1-IV ; ! "вставать" 
оси:оси V-1-IV ; ! "становиться"

бӯ:бу%{ː%} V-2-TV ; ! "давать"
ниэ:ниэ V-2-IV ; ! "выходить"
ӣ:и%{ː%}%{ь%} V-2-IV ; ! "входить"
сӣ:си%{ː%}%{ь%} V-2-IV ; ! "пройти"
чӣ:чи%{ː%}%{ь%} V-2-IV ; ! "отбирать"
ва̄:ва%{ː%} V-2-TV ; ! "убивать"
ва̄йча:ва%{ː%}йча V-1-TV ; ! "убивать"
са̄:са V-2-TV ; ! "знать"

ичэ:ичэ V-1-TV ; ! "видеть"
ичэ:ичэ V-1-IV ; ! "видеть"
ичэди:ичэди V-1-TV ; ! "смотреть"

халачи:халачи V-1-TV ; ! "ждать"
илиси:илиси V-1-IV ; ! "стоять"
тари:тари V-1-TV ; ! "сажать"
пулси:пулси V-1-IV ; ! "ходить"

LEXICON Interjections

э:э INTERJ ; 

LEXICON Ideophones

чоӈдом:чоӈдом IDEO ; 

LEXICON Punctuation 

%.%<sent%>:%. # ;
%:%<sent%>:%: # ;
%,%<cm%>:%, # ;

%.%.%<sent%>:%.%. # ;
%.%.%.%<sent%>:%.%.%. # ;
…%<sent%>:… # ; 
%;%<sent%>:%; # ;
%!%<sent%>:%! # ;
%-%-%<guio%>:%-%- # ;
%-%<guio%>:%- # ;
%−%<guio%>:%− # ; 
%—%<guio%>:%— # ;
%–%<guio%>:%– # ;
%?%<sent%>:%? # ;
%'%<apos%>:%' # ;
%"%<quot%>:%" # ;
%«%<lquot%>:%« # ;
%»%<rquot%>:%» # ;
%”%<rquot%>:%” # ;
%“%<lquot%>:%“ # ;
%(%<lpar%>:%( # ;
%]%<rpar%>:%] # ;
%[%<lpar%>:%[ # ;
%)%<rpar%>:%) # ;
%\%<sent%>:%\ # ;
%/%<sent%>:%/ # ;


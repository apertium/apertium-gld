Alphabet

 а б в г д е ё ж з и й к л м н ӈ о п р с т у ф х ц ч ш щ ъ ы ь э ю я  !!= * @CODE@
 А Б В Г Д Е Ё Ж З И Й К Л М Н Ӈ О П Р С Т У Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я  !!= * @CODE@

!а̄ е̄ ӣ э̄ ӯ о̄
!А̄ Е̄ Ӣ ¯Э Ӯ О̄

 %{A%}:а %{A%}:э
 %{O%}:о %{O%}:у
 %{в%}:в %{в%}:б %{в%}:0

 %{ː%}:̄  !!= * @CODE@

 %>:0  ;

Sets

Vow = а е ё и о у ы э ю я 					
      А Е Ё И О У Ы Э Ю Я ; 

Cns = б в г д ж з й к л м н ӈ п р с т ф х ц ч ш щ ъ ь
      Б В Г Д Ж З Й К Л М Н Ӈ П Р С Т Ф Х Ц Ч Ш Щ Ъ Ь ; 

RealCns = б в г д ж з й к л м н ӈ п р с т ф х ц ч ш щ 
          Б В Г Д Ж З Й К Л М Н Ӈ П Р С Т Ф Х Ц Ч Ш Щ ; 

Rules 

"Vowel harmony for {A}"
%{A%}:Vy <=> :Vx [ :Cns* :RealCns ]/[ :0 | %- ]* _ ;
         where Vx in ( а ё и о у ы э ю я ) 
               Vy in ( а а э а э э э э а ) 
         matched ;

"Vowel harmony for {O}"
%{O%}:Vy <=> :Vx [ :Cns* :RealCns ]/[ :0 | %- ]* _ ;
         except 
             [ :Cns* :RealCns ]/[ :0 | %- ]* _ .#. ;
         where Vx in ( а ё и о у ы э ю я ) 
               Vy in ( о о у о у у у у о )
         matched ;

"Vowel harmony for {O} in final syllable"
%{O%}:у <=> [ :Cns* :RealCns ]/[ :0 | %- ]* _ .#. ;

"Intervocalic {в}"
%{в%}:в <=> :Vow %>: _ :Vow ; 

"Labialisation of {в}"
%{в%}:б <=> [ н: | л: ] %>: _ :Vow ; 

"Assimilation of н to м"
н:м <=> _ %>: :б  ;

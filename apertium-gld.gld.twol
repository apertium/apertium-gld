Alphabet

 а б в г д е ё ж з и й к л м н ӈ о п р с т у ф х ц ч ш щ ъ ы ь э ю я  !!= * @CODE@
 А Б В Г Д Е Ё Ж З И Й К Л М Н Ӈ О П Р С Т У Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я  !!= * @CODE@

!а̄ е̄ ӣ э̄ ӯ о̄
!А̄ Е̄ Ӣ ¯Э Ӯ О̄

 %{A%}:а %{A%}:э
 %{O%}:о %{O%}:у
 %{в%}:в %{в%}:б %{в%}:0
 %{б%}:б %{б%}:0
 %{I%}:й %{I%}:0
 %{н%}:н %{н%}:0
 %{к%}:к %{к%}:ч
 %{i%}:и %{i%}:0
 %{w%}:б %{w%}:в %{w%}:0

 %{ː%}:̄  !!= * @CODE@

 %{ː%}:0 ! FIXME

 %{ъ%}:0  ! Low harmony
 %{ь%}:0  ! High harmony

 %-
 %>:0  ;

Sets

Vow = а е ё и о у ы э ю я 					
      А Е Ё И О У Ы Э Ю Я ; 

Cns = б в г д ж з й к л м н ӈ п р с т ф х ц ч ш щ ъ ь
      Б В Г Д Ж З Й К Л М Н Ӈ П Р С Т Ф Х Ц Ч Ш Щ Ъ Ь ;  

RealCns = б в г д ж з й к л м н ӈ п р с т ф х ц ч ш щ 
          Б В Г Д Ж З Й К Л М Н Ӈ П Р С Т Ф Х Ц Ч Ш Щ ; 

ArchiVow = %{A%} %{O%} ;

Len = %{ː%} ;

LowVow = а ё о я ;

HighVow = у ы э ю ; 

NeutVow = и ;

Rules 

! н:н и:и {ː}:̄ ч:ч э:э н:н >:0 д:д и:и {A}:э д:д и:и

!огда<n><sg><abl>:огда>ди{A}ди
!огда<n><sg><obl>:огда>{в}{A}
!огда<n><pl><loc>:огда>с{A}л>д{O}л{A}

!ниичэн<n><sg><abl>:ни{ː}чэн>ди{A}ди
!ниичэн<n><sg><obl>:ни{ː}чэн>{в}{A}
!ниичэн<n><pl><loc>:ни{ː}чэн>с{A}л>д{O}л{A}

!ихон<n><pl><loc>:ихон>с{A}л>д{O}л{A}

! и:и х:х о:о н:н >:0 с:с {A}:а л:л >:0 д:д {O}:о л:л {A}:а

"High harmony for {A}"
%{A%}:э <=> :HighVow ([ :Cns | :Cns NeutVow ]) %>: [ :Cns | NeutVow | :0 | Len: | :HighVow | %- ]* _ ;
              %{ь%}: ([ :Cns | :Cns NeutVow ]) %>: [ :Cns | NeutVow | :0 | Len: | :HighVow | %- | %{ь%}: ]* _ ;

"Low harmony for {A}"
%{A%}:а <=> :LowVow ([ :Cns | :Cns NeutVow ]) %>: [ :Cns | NeutVow | :0 | Len: | :LowVow | %- ]* _ ;
              %{ъ%}: ([ :Cns | :Cns NeutVow ]) %>: [ :Cns | NeutVow | :0 | Len: | :LowVow | %- | %{ъ%}: ]* _ ;
            
!! Note that vowel harmony is sometimes not completely attested in final syllable -{O} 

! х:х о:о н:н и:и >:0 -:- д:д {A}:а

! о:о г:г д:д а:а >:0 д:д и:и {A}:а д:д и:и

! о:о г:г д:д а:а >:0 п:п {O}:о >:0 в:в {A}:а 

"High harmony for {O}"
%{O%}:у <=> :HighVow ([ :Cns | :Cns NeutVow ]) %>: [ :Cns | NeutVow | :0 | Len: | :HighVow | %- ]* _ ;
              %{ь%}: ([ :Cns | :Cns NeutVow ]) %>: [ :Cns | NeutVow | :0 | Len: | :HighVow | %- | %{ь%}: ]* _ ;

"Low harmony for {O}"
%{O%}:о <=> :LowVow ([ :Cns | :Cns NeutVow ]) %>: [ :Cns | NeutVow | :0 | Len: | :LowVow | %- ]* _ ;
              %{ъ%}: ([ :Cns | :Cns NeutVow ]) %>: [ :Cns | NeutVow | :0 | Len: | :LowVow | %- | %{ъ%}: ]* _ ;

!"Vowel harmony for {O} in final syllable"
!%{O%}:у <=> [ :Cns* :RealCns ]/[ :0 | %- ]* _ .#. ;

"Intervocalic {в}"
%{в%}:в <=> [ :Vow | :й ] %>: _ :Vow ; 

"Labialisation of {в}"
%{в%}:б <=> [ н: | л: ] %>: _ :Vow ; 

"Assimilation of н to м"
н:м <=> _ %>: [ :б | :п ]  ;

"Assimilation of present tense with sg1"
%{I%}:0 <=> _ %>: %{б%}: ;
           и: %>: _ ;
        except 
      .#. б и %>: _ %>: \%{б%}: ;

"Only with verb би-"
%{I%}:и <=> .#. б и %>: _ %>: \%{б%}: ;

"б in sg1 only surfaces after a consonant"
%{б%}:б <=> :Cns %>: _ ; 

! о:о г:г д:д а:а >:0 {б}:в {A}:а р:р и:и

"н surfaces before б" 
%{н%}:м <=> _ %>: %{б%}:  ;
      except
          :н %>: _ ;

"н deletes everywhere there is no б"
%{н%}:0 <=>      _ %>: \%{б%}: ; 
          :н %>: _ ;

"{к} is ч after vowel"
%{к%}:ч <=> :Vow (%{ː%}:) %>: _ ;

"Never delete {i} with surface Cns"
%{i%}:и <=> :Cns _ ; 
           :и %>: %{б%}: _ ; 
           [ :Vow - :и ] %>: %{I%}: %>: %{б%}: _ ; 

"{w} deletion"
%{w%}:0 <=> [ :Vow - [ :о | :у ] ] ([ %{ь%}: | %{ъ%}: ]) %>: _  ; 

